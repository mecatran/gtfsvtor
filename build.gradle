plugins {
  id 'java'
  id 'application'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'GTFSVTOR - General Transit Feed Specification Validator'
version = '1.0.0'
mainClassName = 'com.mecatran.gtfsvtor.cmdline.GtfsVtorMain'
applicationDefaultJvmArgs = [ "-Xmx4G" ]

repositories {
  mavenCentral()
}
startScripts {
    applicationName = 'gtfsvtor'
}
distZip {
    archiveName "gtfsvtor.zip"
}
tasks.distTar.enabled = false

// Return the current commit hash from git
def scmRevisionInfo() {
  new ByteArrayOutputStream().withStream { os ->
    def result = exec {
      executable = 'git'
      args = [ 'rev-parse', 'HEAD' ]
      standardOutput = os
    }
    return os.toString().trim()
  }
}

def utcDateTime() {
  def df = new java.text.SimpleDateFormat("yyyy-MM-dd HH:mm 'UTC'");
  df.setTimeZone(TimeZone.getTimeZone("UTC"));
  return df.format(new Date())
}

jar {
  manifest {
    attributes 'Implementation-Title'  : project.description,
               'Implementation-Version': version,
               'Built-By'              : System.properties['user.name'],
               'Build-Date'            : utcDateTime(),
               'Build-Revision'        : scmRevisionInfo(),
               'Created-By'            : "Gradle ${gradle.gradleVersion}",
               'Build-Jdk'             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
               'Build-OS'              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
  }
}

test {
  testLogging {
    // events "passed", "skipped", "failed", "standardOut", "standardError"
    events "passed", "skipped", "failed", "standardError"
  }
}

dependencies {
  implementation 'com.google.guava:guava:29.0-jre'
  implementation 'org.apache.commons:commons-csv:1.8'
  implementation 'com.univocity:univocity-parsers:2.8.4'
  implementation 'commons-io:commons-io:2.6'
  implementation 'commons-validator:commons-validator:1.6'
  implementation 'org.locationtech.jts:jts-core:1.16.1'
  implementation 'com.googlecode.jatl:jatl:0.2.3'
  implementation 'org.apache.httpcomponents:httpclient:4.5.12'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
  implementation 'com.beust:jcommander:1.78'
  testImplementation 'junit:junit:4.12'
  testImplementation 'io.github.java-diff-utils:java-diff-utils:4.5'
}
